class Cwd {
  use Fn;
  use Sys::IO;
  use Sys;

  static method getcwd : string () {
    my $cwd = Sys::IO->getcwd(undef, 0);
    
    if (Sys->defined("_WIN32")) {
      Fn->replace_chars($cwd, '\\', '/');
    }
    
    return $cwd;
  }
  
  static method realpath : string ($file : string) {
    if (Sys->defined("_WIN32")) {
      my $realpath = Sys::IO->_fullpath(undef, $file, 0);
      
      Fn->replace_chars($realpath, '\\', '/');
      
      my $realpath_length = length $realpath;
      if ($realpath ne "/" && $realpath->[$realpath_length - 1] == '/') {
        Fn->shorten($realpath, $realpath_length - 1);
      }
      
      return $realpath;
    }
    else {
      my $realpath = Sys::IO->realpath($file, undef);
      
      return $realpath;
    }
  }
  
  static method abs_path : string ($file :string) { return &realpath($file); }
  
  static method getdcwd : string ($drive = undef : string) {
    unless (Sys->defined("_WIN32")) {
      set_error_code class_id Error::NotSupported;
      die "The getdcwd is not supported on this system(_WIN32)";
    }
    my $drive_id = 0;
    if ($drive) {
      unless (length $drive == 2) {
        die "The length of the \$drive must be 2";
      }
      
      my $drive_char = $drive->[0];
      unless (($drive_char >= 'A' && $drive_char <= 'Z') || ($drive_char >= 'a' && $drive_char <= 'z')) {
        die "The first character of the \$drive must be [a-zA-Z]";
      }
      unless ($drive->[1]) {
        die "The second character of the \$drive must be \":\"";
      }
      
      my $drive_upper = Fn->ucfirst($drive);
      
      $drive_id = $drive_char - 'A' + 1;
    }
    
    my $cwd = Sys::IO->_getdcwd($drive_id, undef, 1);
    
    Fn->replace_chars($cwd, '\\', '/');
    
    return $cwd;
  }
}
